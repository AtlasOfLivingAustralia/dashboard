buildscript {
    version "2.6-SNAPSHOT"
    group "au.org.ala"
    description "Dashboard application"
}

plugins {
    id "groovy"
    id "org.grails.grails-web"
    id "org.grails.grails-gsp"
    id "application"
    id "war"
    id "maven-publish"
    id "com.bertramlabs.asset-pipeline"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}


publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            artifact bootWar
        }
    }
}

bootWar {
    launchScript()
    dependsOn(compileGroovyPages)
}

war {
    dependsOn(compileGroovyPages)
}

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-dependencies"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:hibernate5:${gormVersion}"
    implementation "org.hibernate:hibernate-core:5.6.15.Final"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    implementation "org.codehaus.groovy:groovy-dateutil"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:4.3.0"
    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-safari-driver:$seleniumSafariDriverVersion"

    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testImplementation "org.grails:grails-web-testing-support"

    implementation 'dk.glasius:external-config:3.1.1'
    runtimeOnly 'org.apache.ant:ant:1.7.1'    //you can also use runtime
    runtimeOnly 'org.apache.ant:ant-launcher:1.7.1'
    implementation 'org.grails.plugins:csv:1.0.ALA.3'

    implementation 'com.yahoo.platform.yui:yuicompressor:2.4.8'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.9.1'
    implementation group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.2'
    implementation 'org.codehaus.groovy:groovy-ant:2.0.0'

    implementation "org.grails.plugins:ala-bootstrap3:4.4.0"
    implementation "org.grails.plugins:grails-google-visualization:2.0"
    implementation "org.grails.plugins:ala-admin-plugin:2.3.0"
    implementation "org.grails.plugins:ala-auth:6.2.0"
    implementation 'org.grails.plugins:quartz:2.0.13'
    implementation 'org.quartz-scheduler:quartz:2.3.2' // Is not pulled in by default

    implementation group: 'com.opencsv', name: 'opencsv', version: '5.7.1'
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}


assets {
    minifyJs = false
    minifyCss = false
}

